import "util.scrypt";

// a fair secure multiparty lottery
// each player i chooses a random number n_i and the winner is the w-th player
// where w = (n_0 + n_1 + .. + n_(N-1)) mod N
contract Lottery {
    // number of players
    static const int N = 5;
    // players identified by their addresses
    PubKey[N] players;
    // commitments: the hash of their random numbers
    Sha256[N] nonceHashes;
    
    public function reveal(int[N] nonces, Sig sig) {
        int i = 0;
        int sum = 0;
        loop (N) {
            // open commit
            require(hash256(pack(nonces[i])) == this.nonceHashes[i]);
            sum += nonces[i];
            i++;
        }
        
        PubKey winner = this.players[sum % N];

        require(checkSig(sig, winner));
    }
}
import "util.scrypt";
import "rabin.scrypt";

contract WitnessBinaryOption {
  bytes symbol;
  int decimal;
  int betPrice;
  int matureTime;
  int witnessPubKey;
  Ripemd160 pubKeyHashA;
  Ripemd160 pubKeyHashB;

  public function unlock(SigHashPreimage txPreimage, int price, int unlockTime, int witnessSig, bytes witnessPadding, int outAmount) {

    // check preimage
    require(Tx.checkPreimage(txPreimage));

    require(unlockTime >= this.matureTime);
    // require(unlockTime <= this.matureTime + 3600);

    RabinSignature rabin = new RabinSignature();
    //symbol+price(BigInt 32bytes)+decimal(UInt8)+timestamp(UInt32)
    bytes msg = this.symbol + num2bin(price, 32) + num2bin(this.decimal, 1) + num2bin(unlockTime, 4);
    rabin.verifySig(witnessSig, msg, witnessPadding, this.witnessPubKey);

    // check price
    bytes outputScript = b'';
    if (price >= this.betPrice) {
      outputScript = Util.buildPublicKeyHashScript(this.pubKeyHashA);
    } else {
      outputScript = Util.buildPublicKeyHashScript(this.pubKeyHashB);
    }

    // check the output is the right winner
    bytes outputHash = hash256(Util.buildOutput(outputScript, outAmount));
    bytes outputHash2 = Util.hashOutputs(txPreimage);
    require(outputHash == outputHash2);
  }
}

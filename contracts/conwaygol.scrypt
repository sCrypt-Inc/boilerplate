import "util.scrypt";

// Conway Game Of Life on a board of N * N
contract GameOfLife {
    static const int N = 5;
    // effctively we play on a grid of (N+2) * (N+2) without handling boundary cells
    static int BOARD_SIZE = GameOfLife.N + 2;
    static bytes LIVE = b'01';
    static bytes DEAD = b'00';
    static const int LOOP_NEIGHBORS = 3;

    public function play(int amount, SigHashPreimage txPreimage) {
        require(Tx.checkPreimage(txPreimage));

        bytes scriptCode = Util.scriptCode(txPreimage);
        int scriptLen = len(scriptCode);

        int BOARDLEN = GameOfLife.BOARD_SIZE * GameOfLife.BOARD_SIZE;
        int boardStart = scriptLen - BOARDLEN;
        bytes oldBoard = scriptCode[boardStart : ];

        // make the move
        bytes newBoard = this.evolve(oldBoard);

        // update state: next turn & next board
        bytes scriptCode_ = scriptCode[ : scriptLen - BOARDLEN] + newBoard;
        bytes output = Util.buildOutput(scriptCode_, amount);
        bytes outputs = output;

        require(hash256(outputs) == Util.hashOutputs(txPreimage));
    }

    function evolve(bytes oldBoard) : bytes {
        bytes newBoard = oldBoard;

        int i = 1;
        loop (GameOfLife.N) {
            int j = 1;
            loop (GameOfLife.N) {
                bytes nextState = this.next(oldBoard, i, j);
                newBoard = Util.setElemAt(newBoard, this.index(i, j), nextState);

                j++;
            }

            i++;
        }

        return newBoard;
    }

    function next(bytes oldBoard, int row, int col) : bytes {
        // number of neighbors alive
        int alive = 0;

        int i = - 1;
        loop (LOOP_NEIGHBORS) {
            int j = - 1;

            loop (LOOP_NEIGHBORS) {
                if (!(i == 0 && j == 0)) {
                    if (Util.getElemAt(oldBoard, this.index(row + i, col + j))) {
                        alive++;
                    }
                }
                j++;
            }

            i++;
        }

        bytes oldState = Util.getElemAt(oldBoard, this.index(row, col));
        /* rule
        1. Any live cell with two or three live neighbours survives.
        2. Any dead cell with three live neighbours becomes a live cell.
        3. All other live cells die in the next generation. Similarly, all other dead cells stay dead.
        */
        return(alive == 3 || alive == 2 && oldState == LIVE) ? LIVE : DEAD;
    }

    function index(int i, int j) : int {
        return i * GameOfLife.BOARD_SIZE + j;
    }
}

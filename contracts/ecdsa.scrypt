import "ecVerify.scrypt";

struct RSPair {
    int r;
    int s;
}

// ECDSA signatures verification for secp256k1, for arbitrary message @msg
contract ECDSA {
    public function verify(Sig sig, PubKey pubKey, bytes msg,
        int invS, Point P, int lambda, Point U1, PointMulAux u1Aux, Point U2, PointMulAux u2Aux) {

        // extract (r, s) from sig
        RSPair rs = parseDERSig(sig);
        int r = rs.r;
        int s = rs.s;
        // within range
        require(r >= 1 && r < ECVerify.n);
        require(s >= 1 && s < ECVerify.n);

        // verify invS
        require((s * invS) % ECVerify.n == 1);
        
        int e = unpack(sha256(msg));
        int u1 = (e * invS) % ECVerify.n;
        int u2 = (r * invS) % ECVerify.n;

        // U1 = u1 * G
        require(ECVerify.isMul(ECVerify.G, u1, U1, u1Aux));

        Point Q = ECVerify.pubKey2Point(pubKey);
        // U2 = u2 * Q
        require(ECVerify.isMul(Q, u2, U2, u2Aux));

        // P == U1 + U2
        require(ECVerify.isSum(U1, U2, lambda, P));
        // cannot be identify
        require(P != ECVerify.ZERO);

        require((P.x - r) % ECVerify.n == 0);
    }

    // parse signature in DER format to get (r, s) pair
    static function parseDERSig(Sig sig) : RSPair {
        int rLen = unpack(sig[3 : 4]);
        int r = fromBESigned(sig[4 : 4 + rLen]);

        int sLen = unpack(sig[6 + rLen : 7 + rLen]);
        int s = fromBESigned(sig[7 + rLen : 7 + rLen + sLen]);

        return { r , s };
    }

    // r & s are signed big endian
    static function fromBESigned(bytes b) : int {
        // convert big-endian to little-endian: either 32 or 33 bytes
        bytes bLE = len(b) == 32 ? reverseBytes(b, 32) : reverseBytes(b, 33);
        return unpack(bLE);
    }
}

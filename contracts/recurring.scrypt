
contract Recurring {
    Ripemd160 userPubKeyHash; //Address of the owner of the coin
    int merchantPayment; //How much the merchant can take from the contract
    Ripemd160 merchantPubKeyHash; //Address of the merchant
    int frequenceOfPayment; //Merchant can take money each "frequenceOfPayment" seconds

    // User can deposit more money in his recurring contract
    public function deposit_user(SigHashPreimage preImage, int depositAmount, int changeAmount) {
        require(Tx.checkPreimage(preImage));
        require(depositAmount > 0);
        int contractTotalAmount = SigHash.value(preImage) + depositAmount;
        bytes output = Utils.buildOutput(SigHash.scriptCode(preImage), contractTotalAmount);
        bytes change_output = changeAmount <= 546 ? b'' : Utils.buildOutput(Utils.buildPublicKeyHashScript(this.userPubKeyHash), changeAmount);
        require(hash256(output + change_output) == SigHash.hashOutputs(preImage));
    }

    // User wants to withdraw its money. Standard P2PKH transaction
    public function withdraw_user(Sig sig, PubKey pubKey) {
        require(hash160(pubKey) == this.userPubKeyHash);
        require(checkSig(sig, pubKey));
    }

    // Merchant wants its money
    public function withdraw_merchant(SigHashPreimage preImage, int newAmount) {
        require(Tx.checkPreimage(preImage));
        //Output for the merchant
        bytes outputMerchant = Utils.buildOutput(Utils.buildPublicKeyHashScript(this.merchantPubKeyHash), this.merchantPayment);
        //Output that'll be the next instance of the contract
        //Check nlocktime
        bytes scriptCode = SigHash.scriptCode(preImage);
        int scriptLen = len(scriptCode);
        bytes codePart = this.getCodePart(scriptCode, scriptLen);
        int newMatureTime = this.getPrevMatureTime(scriptCode, scriptLen) + this.frequenceOfPayment;
        bytes newScript = codePart + pack(newMatureTime);
        require(SigHash.nLocktime(preImage) == newMatureTime);
        //Build the output
        bytes outputContract = Utils.buildOutput(newScript, newAmount);
        require(hash256(outputContract + outputMerchant) == SigHash.hashOutputs(preImage));
    }

    function getPrevMatureTime(bytes lockingScript, int scriptLen) : int {
        return unpack(lockingScript[scriptLen - 4 :]);
    }

    function getCodePart(bytes lockingScript, int scriptLen) : bytes {
        return lockingScript[0 : scriptLen - 4];
    }
}
